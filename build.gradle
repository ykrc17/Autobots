buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.71"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

apply plugin: "kotlin"
apply plugin: "com.github.johnrengelman.shadow"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation([
            "org.javassist:javassist:3.23.1-GA",
    ])

    compileOnly([
            gradleApi(),
            "com.android.tools.build:gradle:2.3.3",
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
    ])
}

/**
 *              gradle 3.0  kotlin 1.1
 * android 2.3  gradle 3.3  kotlin 1.1
 *              gradle 4.5  kotlin 1.2
 * android 3.2  gradle 4.6  kotlin 1.2
 *              gradle 5.0  kotlin 1.3
 */
compileKotlin {
    kotlinOptions {
        languageVersion = "1.1"
        apiVersion = "1.1"
    }
}

def pluginId = "autobots"
def pluginClass = "com.ykrc17.gradle.autobots.AutobotsPlugin"

shadowJar {
    classifier = null
    metaInf {
        def tmpDir = file("$buildDir/tmp/META-INF")
        doFirst {
            file("$tmpDir/gradle-plugins/${pluginId}.properties").with {
                parentFile.mkdirs()
                write("implementation-class=${pluginClass}")
            }
        }
        from tmpDir
        doLast {
            delete tmpDir
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8