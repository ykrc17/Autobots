buildscript {
    ext.kotlin_version = "1.3.0"

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
        classpath files("meta-plugin.jar")
    }
}

apply plugin: "kotlin"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "meta-plugin"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation([
            "org.javassist:javassist:3.23.1-GA",
    ])

    compileOnly([
            "com.android.tools.build:gradle:2.3.3",
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
    ])
}

/**
 *              gradle 3.0  kotlin 1.1
 * android 2.3  gradle 3.3  kotlin 1.1
 *              gradle 4.5  kotlin 1.2
 * android 3.2  gradle 4.6  kotlin 1.2
 *              gradle 5.0  kotlin 1.3
 */
compileKotlin {
    kotlinOptions {
        languageVersion = "1.1"
        apiVersion = "1.1"
    }
}

metaPlugin {
    plugin {
        id = "clew"
        implClass = "com.ykrc17.gradle.clew.ClewPlugin"
    }
    plugin {
        id = "com.ykrc17.clew"
        implClass = "com.ykrc17.gradle.clew.ClewPlugin"
    }
}

shadowJar {
    classifier = null
    System.getProperty("os.name").with {
        if (it.startsWith("Mac")) {
            doLast { exec { commandLine "open", "$buildDir/libs" } }
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8